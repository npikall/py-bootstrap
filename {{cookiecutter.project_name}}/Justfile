# Default command lists all available recipes
_default:
    @just --list

alias b := bump
alias c := clean
alias d := dist
alias h := hooks
alias q := check
alias t := test

PROJ_VERSION := `uv version --short`
PY_VERSION := `cat .python-version`

# run all the formatting, linting, and testing commands
ci PYTHON=PY_VERSION:
    uv run --python={{ PYTHON }} --dev ruff format .
    uv run --python={{ PYTHON }} --dev ruff check . --fix
    uv run --python={{ PYTHON }} --dev ty check .
    uv run --python={{ PYTHON }} --dev --group test pytest tests/

# run all the tests for all the supported Python versions
testall:
    uv run --python=3.10 --group test pytest
    uv run --python=3.11 --group test pytest
    uv run --python=3.12 --group test pytest
    uv run --python=3.13 --group test pytest

# print the current status of the project
status:
    @echo "Project Version: {{ PROJ_VERSION }}"
    @echo "Python Version: {{ PY_VERSION }}"
    @echo "Running on: `uname`"

# clean all python build/compilation files and directories
clean: clean-build clean-pyc clean-test

# remove build artifacts
clean-build:
    rm -fr build/
    rm -fr dist/
    rm -fr .eggs/
    find . -name '*.egg-info' -exec rm -fr {} +
    find . -name '*.egg' -exec rm -f {} +

# remove Python file artifacts
clean-pyc:
    find . -name '*.pyc' -exec rm -f {} +
    find . -name '*.pyo' -exec rm -f {} +
    find . -name '*~' -exec rm -f {} +
    find . -name '__pycache__' -exec rm -fr {} +

# remove test and coverage artifacts
clean-test:
    rm -f .coverage
    rm -fr htmlcov/
    rm -fr .pytest_cache

# install dependencies in local venv
venv:
    uv sync --group test --group dev

# run test with coverage
test:
    uv run pytest tests/ -rsx --verbose --color=yes

# lint python code using ruff
[private]
lint:
    uvx ruff check . --fix

# format python code using ruff
[private]
format:
    uvx ruff format .

# run the type checker ty
[private]
types:
    uvx ty check

# lint and format python code (ruff) and run the type checker (ty)
check: lint format types

# setup the pre-commit hooks
hooks:
    uvx pre-commit install

[confirm("Do you really want to bump? (y/n)")]
[private]
prompt-confirm:

# bump the version, commit the changes and add a tag (increment can be major, minor, patch,...)
bump INCREMENT: && tag
    @uv version --bump {{ INCREMENT }} --dry-run
    @just prompt-confirm
    uv version --bump {{ INCREMENT }}

# tag the latest version
tag VERSION=`uv version --short`:
    git add pyproject.toml
    git commit -m "Bumped version to {{VERSION}}"
    git tag "v{{VERSION}}"
    @echo "{{ GREEN }}{{ BOLD }}Version has been bumped to {{VERSION}}.{{ NORMAL }}"

# build the source distribution and wheel file with uv (bump version first)
dist:
    uv build

# initialize a git repo and add all files
init:
    git init
    git add .
    git commit -m "initial commit"
    @echo "{{ GREEN }}{{ BOLD }}Git has been initialized{{ NORMAL }}"
